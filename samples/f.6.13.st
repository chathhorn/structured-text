FUNCTION_BLOCK TRANSFER
 VAR_INPUT
   AUTO : BOOL ;
   XIN : REAL ;
   FASTRATE, SLOWRATE : REAL ;
   FASTUP, SLOWUP,
   FASTDOWN, SLOWDOWN : BOOL;
   CYCLE : TIME ;
 END_VAR
 VAR_OUTPUT XOUT : REAL ; END_VAR
 VAR XFERRAMP : INTEGRAL ;
     RAMPRATE : REAL ;
 END_VAR
 RAMPRATE := 0.0 ;
 IF NOT AUTO THEN
  IF FASTUP THEN RAMPRATE := FASTRATE; END_IF;
  IF SLOWUP THEN RAMPRATE := RAMPRATE + SLOWRATE; END_IF;
  IF FASTDOWN THEN RAMPRATE := RAMPRATE - FASTRATE; END_IF;
  IF SLOWDOWN THEN RAMPRATE := RAMPRATE - SLOWRATE; END_IF;
 END_IF ;
 XFERRAMP (RUN := 1, CYCLE := CYCLE, R1 := AUTO,
            XIN := RAMPRATE, X0 := XIN) ;
 XOUT := XFERRAMP.XOUT;
END_FUNCTION_BLOCK